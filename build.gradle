buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3'
	}
}

apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: "java"
apply plugin: 'maven'
apply plugin: 'signing'

if(project.hasProperty('ossrhUser') && project.hasProperty("release")) {
	apply plugin: "io.codearte.nexus-staging"

	nexusStaging {
		username = ossrhUser
		password = ossrhPassword
	}
}

group = 'org.mini2Dx'
version = '1.0.0-SNAPSHOT'
description = 'Cross-platform OS detection and native library loading for Java'

repositories {
	mavenLocal()
	mavenCentral()
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

if (JavaVersion.current().isJava8Compatible()) {
	allprojects {
		tasks.withType(Javadoc) {
			options.addStringOption('Xdoclint:none', '-quiet')
		}
	}
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar
}

task docs(type: Javadoc) {
	source project.sourceSets.main.allJava
	classpath = files(project.sourceSets.main.compileClasspath)
	destinationDir = new File(project.projectDir, 'docs')
}
docs.dependsOn build

if(project.hasProperty('ossrhUser')) {
	signing {
		sign configurations.archives
	}
	
	uploadArchives {
		repositories {
			mavenDeployer {
				// POM signature
				beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
				// Target repository
				repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
					authentication(userName: ossrhUser, password: ossrhPassword)
				}
				pom.project {
					name project.name
					description project.description
					packaging 'jar'
					url 'https://github.com/mini2Dx/natives-loader'

					scm {
						connection 'scm:git:https://github.com/mini2Dx/natives-loader.git'
						developerConnection 'scm:git:git@github.com:mini2Dx/natives-loader.git'
						url 'https://github.com/mini2Dx/natives-loader.git'
					}

					licenses {
						license {
							name 'Apache 2.0 License'
							url 'https://opensource.org/licenses/Apache-2.0'
							distribution 'repo'
						}
					}

					developers {
						developer {
							id = 'tomcashman'
							name = 'Thomas Cashman'
							email = 'thomascashman404@gmail.com'
						}
					}
				}
			}
		}
	}
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.2.1'
}